# check initilize speed: `time zsh -i -c exit`
# zmodload zsh/zprof

PATH=$HOME/bin:$PATH

eval "$(sheldon source)"

# ヒストリー機能
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000
setopt hist_ignore_all_dups # 重複するコマンド行は古い方を削除
setopt hist_ignore_dups     # 直前と同じコマンドラインはヒストリに追加しない
setopt share_history        # コマンド履歴ファイルを共有する
setopt append_history       # 履歴を追加 (毎回 .zsh_history を作るのではなく)
setopt inc_append_history   # 履歴をインクリメンタルに追加
setopt hist_no_store        # historyコマンドは履歴に登録しない
setopt hist_reduce_blanks   # 余分な空白は詰めて記録
setopt auto_cd              # ディレクトリ名だけで移動

autoload -Uz edit-command-line
zle -N edit-command-line

bindkey -e                           # emacsキーバインドを有効に
bindkey "^[[Z" reverse-menu-complete # Shift-Tabで補完候補を逆順する("\e[Z"でも動作する)
bindkey "^[[3~" delete-char          # Deleteキーで文字を削除する
bindkey "^[[1;3C" forward-word       # Option + 右矢印で単語単位でカーソル移動 (Option + f と同じ)
bindkey "^[[1;3D" backward-word      # Option + 左矢印で単語単位でカーソル移動 (Option + b と同じ) 
bindkey "^[q" push-line              # Option + Q で行をスタックに保存
bindkey "^[e" edit-command-line      # Option + E でコマンドライン編集モードに入る

# 色の設定
export LSCOLORS=Exfxcxdxbxegedabagacad
# 補完時の色の設定
export LS_COLORS='di=01;34:ln=01;35:so=01;32:ex=01;31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'
export ZLS_COLORS=$LS_COLORS
export CLICOLOR=true
# 補完候補に色を付ける
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:default' menu select=1 # 補完候補のカーソル選択を有効に

# OSC 133
_prompt_executing=""
function __prompt_precmd() {
    local ret="$?"
    if test "$_prompt_executing" != "0"; then
        _PROMPT_SAVE_PS1="$PS1"
        _PROMPT_SAVE_PS2="$PS2"
        PS1=$'%{\e]133;P;k=i\a%}'$PS1$'%{\e]133;B\a\e]122;> \a%}'
        PS2=$'%{\e]133;P;k=s\a%}'$PS2$'%{\e]133;B\a%}'
    fi
    if test "$_prompt_executing" != ""; then
        printf "\033]133;D;%s;aid=%s\007" "$ret" "$$"
    fi
    printf "\033]133;A;cl=m;aid=%s\007" "$$"
    _prompt_executing=0
}
function __prompt_preexec() {
    PS1="$_PROMPT_SAVE_PS1"
    PS2="$_PROMPT_SAVE_PS2"
    printf "\033]133;C;\007"
    _prompt_executing=1
}
preexec_functions+=(__prompt_preexec)
precmd_functions+=(__prompt_precmd)

if type brew &>/dev/null; then
    FPATH=$HOMEBREW_PREFIX/share/zsh-completions:$FPATH
    FPATH=$HOMEBREW_PREFIX/share/zsh/site-functions:${FPATH}

    autoload -Uz compinit
    _zcompdump="${XDG_CACHE_HOME:-${HOME}/.cache}/zsh/.zcompdump-${HOST}-${ZSH_VERSION}"
    mkdir -p "${_zcompdump:h}"

    setopt extendedglob
    if [[ -n "${_zcompdump}"(#qN.mh+24) ]]; then
        compinit -d "${_zcompdump}"
    else
        compinit -C -d "${_zcompdump}"
    fi
fi

eval "$(mise activate zsh)" # mise は _evalcache 使うと npm で入れてるツールとかが動かなくなる
_evalcache starship init zsh

[ -f ~/.config/zsh/fzf.zsh ] && source ~/.config/zsh/fzf.zsh

# Neovim があれば、VISUAL と EDITOR を設定
if command -v nvim >/dev/null 2>&1; then
    export VISUAL=nvim
    export EDITOR="$VISUAL"
fi

# Java
export JAVA_HOME=$(/usr/libexec/java_home)
export PATH="$JAVA_HOME/bin:$PATH"

# Go
export GOPATH=$HOME/go
export GO111MODULE=on
export PATH="$GOROOT/bin:$PATH"
export PATH="$PATH:$GOPATH/bin"

# Rust
export PATH="$HOME/.cargo/bin:$PATH"

# GCloud
zsh-defer source "$HOMEBREW_PREFIX/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc"
zsh-defer source "$HOMEBREW_PREFIX/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc"

# Deno
export PATH="$HOME/.deno/bin:$PATH"

# pnpm
export PNPM_HOME="$HOME/Library/pnpm"
export PATH="$PNPM_HOME:$PATH"

[ -f ~/.config/zsh/completion-for-pnpm.zsh ] || pnpm completion zsh >~/.config/zsh/completion-for-pnpm.zsh
source ~/.config/zsh/completion-for-pnpm.zsh

# alias
[ -f ~/.config/zsh/alias.zsh ] && source ~/.config/zsh/alias.zsh

# zoxide
zsh-defer _evalcache zoxide init zsh

function ghq-fzf() {
    local selected_dir=$(ghq list | fzf --query="$LBUFFER")

    if [ -n "$selected_dir" ]; then
        BUFFER="cd $(ghq root)/${selected_dir}"
        zle accept-line
    fi

    zle reset-prompt
}

zle -N ghq-fzf
bindkey "^g" ghq-fzf


# Option + P でファイル選択（プレビュー付き）
fzf-file-widget-preview() {
  LBUFFER+=$(find . -type f 2>/dev/null | fzf --preview 'bat --color=always --style=numbers --line-range=:500 {}' --preview-window 'right:50%:wrap')
  zle redisplay
}
zle -N fzf-file-widget-preview
bindkey '^[p' fzf-file-widget-preview 

# tabtab source for packages
# uninstall by removing these lines
[[ -f ~/.config/tabtab/zsh/__tabtab.zsh ]] && . ~/.config/tabtab/zsh/__tabtab.zsh || true

# terminfo
if [ -d ~/.terminfo ]; then
    export TERM=wezterm
    # export TERM=ghostty
fi

if [ -f ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi

# zprof
